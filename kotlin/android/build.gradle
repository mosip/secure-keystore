apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.sonarqube'
apply plugin: 'maven-publish'

buildscript {
  ext.kotlin_version = rootProject.ext.has('kotlinVersion') ? rootProject.ext.get('kotlinVersion') : project.properties['SecureKeystore_kotlinVersion']

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:4.2.0'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.3"
  }
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['SecureKeystore_' + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties['SecureKeystore_' + name]).toInteger()
}

android {
  compileSdkVersion getExtOrIntegerDefault('compileSdkVersion')

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault('minSdkVersion')
    targetSdkVersion getExtOrIntegerDefault('targetSdkVersion')
  }
  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable 'GradleCompatible'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  google()
  mavenCentral()
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation 'com.madgag.spongycastle:core:1.56.0.0'
  implementation 'com.madgag.spongycastle:prov:1.56.0.0'
  implementation 'com.madgag.spongycastle:bcpkix-jdk15on:1.56.0.0'
  implementation "androidx.biometric:biometric:1.1.0"
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'
  implementation 'androidx.security:security-crypto:1.1.0-alpha05'
}

//TODO: To be removed once compile sdk version update is done
//Temp fix to avoid Secure keystore library build failure due to compileSdk of it lesser than one of its dependency (androidx.security:security-crypto:1.1.0-alpha05)
tasks.whenTaskAdded { task ->
  if (task.name == "checkDebugAndroidTestAarMetadata") {
    task.enabled = false
  }
}

sonarqube {
  properties {
    property "sonar.java.binaries", "build/intermediates/javac/debug"
    property "sonar.language", "kotlin"
    property "sonar.exclusions", "**/build/**, **/*.kt.generated, **/R.java, **/BuildConfig.java"
  }
}

apply from: "publish-artifact.gradle"
